/*******************************************************************
* Client: AIRIS PHARMA Private Limited.                                                           
* Product:                                                   
* Project: Protocol 043-1810                                                  
* Program: tab2_1_AH_20221001.SAS  
*
* Program Type: Listing
*
* Purpose: To produce the Table 14.1.7  Subject Demographics - Age (DLT Evaluable Population)
* Usage Notes: 
*
* SAS® Version: 9.4 [TS2M0]
* Operating System: Windows 11 Standard Edition.                   
*
* Author: Andrew Huang
* Date Created: Oct 01, 2022
*******************************************************************/

LIBNAME adam "E:\ROSHE30730\ADAM DATASETS";
LIBNAME sdtm "E:\ROSHE30730\SDTM DATASETS";

%include "E:\ROSHE\PROGRAMS\_RTFSTYLE_.sas";
%_RTFSTYLE_;

DATA adsl;
SET adam.ADSL;
IF TRT01P NE '' AND DLTEVLFL="Y";
OUTPUT;
TRT01P="Overall";
TRT01PN=5;
OUTPUT;
KEEP USUBJID TRT01P TRT01PN SAFFL DLTEVLFL AGE;
RUN;

PROC SQL NOPRINT;
CREATE TABLE TRT AS
SELECT TRT01PN, TRT01P, COUNT(DISTINCT USUBJID) AS DENOM FROM ADSL
GROUP BY TRT01PN, TRT01P;
QUIT;

DATA DUMMY;
DO TRT01PN = 1 TO 5;
OUTPUT;
END;
RUN;

DATA DUMMY1;
SET DUMMY;
DENOM=0;
RUN;

DATA TRT1;
MERGE DUMMY1 TRT;
BY TRT01PN;
RUN;

PROC SQL;
SELECT DENOM INTO:n1 - :n5 FROM TRT1;
QUIT;
%PUT &n1 &n2 &n3 &n4 &n5;

PROC SUMMARY DATA=ADSL NWAY;
CLASS TRT01PN TRT01P;
VAR AGE;
OUTPUT OUT=ADSL2
N=N MEAN=MEAN MEDIAN=MEDIAN MIN=MIN MAX=MAX STD=STD;
RUN;

DATA ADSL3;
SET ADSL2;

CN=LEFT(PUT(N,3.));
CMEAN=LEFT(PUT(MEAN,4.1));
CMEDIAN=LEFT(PUT(MEDIAN,4.1));
CSTD=LEFT(PUT(STD,5.2));
CMIN=LEFT(PUT(MIN,3.));
CMAX=LEFT(PUT(MAX,3.));

RUN;

PROC TRANSPOSE DATA=ADSL3 OUT=ADSL4 PREFIX=T;
ID TRT01PN;
VAR CN CMEAN CMEDIAN CSTD CMIN CMAX;
RUN;

DATA ADSL5;
SET ADSL4;
LENGTH STAT $100.;

IF _NAME_="CN" THEN DO; STAT="N"; OD=1; END;
IF _NAME_="CMEAN" THEN DO; STAT="Mean"; OD=2; END;
IF _NAME_="CSTD" THEN DO; STAT="SD"; OD=3; END;
IF _NAME_="CMEDIAN" THEN DO; STAT="Median"; OD=4; END;
IF _NAME_="CMIN" THEN DO; STAT="Minimum"; OD=5; END;
IF _NAME_="CMAX" THEN DO; STAT="Maximum"; OD=6; END;

RUN;

DATA LABEL;
LENGTH STAT $100.;
STAT="Age (Years)";
OD=0;
RUN;

DATA ADSL6;
SET LABEL ADSL5;
RUN;

PROC SORT DATA=ADSL6 OUT=ADSL6;
BY OD;
RUN;

DATA DUM;
SET ADSL6 (KEEP=OD);
LENGTH T1 T2 T3 T4 T5 $5.;
T1 = " ";
T2 = " ";
T3 = " ";
T4 = " ";
T5 = " ";
RUN;

DATA ADSL7;
MERGE DUM ADSL6;
BY OD;
IF STAT = "N" THEN DO;
	IF T1="" THEN T1="  0";
	IF T4="" THEN T4="  0";
END;
RUN;

TITLE1 J=L "AIRIS PHARMA Private Limited.";
TITLE2 J=L "Protocol: 043-1810";
TITLE3 J=C "Table 14.1.7  Subject Demographics - Age (DLT Evaluable Population)";
FOOTNOTE1 J=L "E:\ROSHE30730\PROGRAMS\TAB3_2_AH_20221001.SAS";
OPTIONS ORIENTATION=LANDSCAPE;
ODS ESCAPECHAR="^";

ODS RTF FILE ="E:\ROSHE30730\OUTPUTS\14_1_7.rtf" STYLE=styles.test;

PROC REPORT DATA=ADSL7 NOWD MISSING
STYLE = {OUTPUTWIDTH=100%} SPLIT="|" SPACING=1 WRAP
STYLE (HEADER) = [JUST=L];

COLUMN OD STAT T1 T2 T3 T4 T5;

DEFINE OD/ORDER NOPRINT;
DEFINE STAT/DISPLAY "CATEGORY"
STYLE (HEADER) = [JUST=LEFT CELLWIDTH=10%]
STYLE (COLUMN) = [JUST=LEFT CELLWIDTH=10%];

DEFINE T1/DISPLAY "DRUG A|(N = &n1)"
STYLE (HEADER) = [JUST=LEFT CELLWIDTH=10%]
STYLE (COLUMN) = [JUST=LEFT CELLWIDTH=10%];

DEFINE T2/DISPLAY "DRUG B|(N = &n2)"
STYLE (HEADER) = [JUST=LEFT CELLWIDTH=10%]
STYLE (COLUMN) = [JUST=LEFT CELLWIDTH=10%];

DEFINE T3/DISPLAY "DRUG C|(N = &n3)"
STYLE (HEADER) = [JUST=LEFT CELLWIDTH=10%]
STYLE (COLUMN) = [JUST=LEFT CELLWIDTH=10%];

DEFINE T4/DISPLAY "DRUG D|(N = &n4)"
STYLE (HEADER) = [JUST=LEFT CELLWIDTH=10%]
STYLE (COLUMN) = [JUST=LEFT CELLWIDTH=10%];

DEFINE T5/DISPLAY "ALL|(N = &n5)"
STYLE (HEADER) = [JUST=LEFT CELLWIDTH=10%]
STYLE (COLUMN) = [JUST=LEFT CELLWIDTH=10%];

COMPUTE BEFORE _PAGE_;
LINE @1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPCOLOR=BLACK BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

COMPUTE AFTER _PAGE_;
LINE @1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPCOLOR=BLACK BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;
RUN;

ODS _ALL_ CLOSE;
