/*******************************************************************
* Client: AIRIS PHARMA Private Limited.                                                           
* Product:                                                   
* Project: Protocol 043-1810                                                  
* Program: TAB9_1_AH_20221001.SAS  
*
* Program Type: Table
*
* Purpose: To produce the Table Table 14.1.22  Best overall response (Safety Population)
* Usage Notes: 
*
* SAS® Version: 9.4 [TS2M0]
* Operating System: Windows 11 Standard Edition.                   
*
* Author: Andrew Huang
* Date Created: Oct 01, 2022
*******************************************************************/

LIBNAME adam "E:\ROSHE30730\ADAM DATASETS";
LIBNAME sdtm "E:\ROSHE30730\SDTM DATASETS";

%include "E:\ROSHE\PROGRAMS\TABLE_POP.sas";
%include "E:\ROSHE\PROGRAMS\_RTFSTYLE_.sas";
%_RTFSTYLE_;

DATA ADRS;
SET ADAM.ADRS;
IF AVAL NE .;
RUN;

DATA ADRS;
MERGE ADRS ADAM.ADSL;
BY USUBJID;
RUN;

DATA ADRS;
SET ADTS;
IF TRT01PN IN (1,2) AND AVAL NE. AND AVAL NOT IN (0,1);
RUN;

PROC SORT DATA=ADRS OUT=ADRS;
BY TRT01PN PARAMCD PARAM AVAL;
RUN;

PROC FREQ DATA=ADRS;
BY TRT01PN;
TABLES PARAMCD*PARAM*AVAL*AVALC/OUT=AVAL_C (DROP=PERCENT);
RUN;

DATA ADSL;
SET ADAM.ADSL;
IF TRT01PN NE '' AND SAFFL="Y";
OUTPUT;
KEEP USUBJID TRT01P TRT01PN SAFFL DLTEVLFL ENRLFL SEX RACE;
RUN;

PROC SQL NOPRINT;
CREATE TABLE TRT AS
SELECT TRT01PN, TRT01P, COUNT(DISTINCT(USUBJID)) AS DENOM FROM ADSL
GROUP BY TRT01PN, TRT01P
ORDER BY TRT01PN, TRT01P;

SELECT DENOM INTO: n1 -: n4 FROM TRT;
QUIT;

%PUT &n1 &n2 &n3 &n4;

DATA FINAL;
SET AVAL_C;
NN=COUNT;
TRTPN=TRT01PN;
LENGTH GRPA_1 $20.;

IF TRTPN=1 THEN DO;
	IF NN=. OR NN=0 THEN GRPA_1="0";
	ELSE IF NN=&n1 THEN GRPA_1=COMPRESS(NN)||" (100)";
	ELSE GRPA_1=PUT(NN,3.)||" ("||PUT(NN/&n1*100,4.1)||")";
END;

IF TRTPN=2 THEN DO;
	IF NN=. OR NN=0 THEN GRPA_1="0";
	ELSE IF NN=&n2 THEN GRPA_1=COMPRESS(NN)||" (100)";
	ELSE GRPA_1=PUT(NN,3.)||" ("||PUT(NN/&n2*100,4.1)||")";
END;

RUN;

PROC SORT DATA=FINAL OUT=FINAL;
BY PARAMCD PARAM AVAL AVALC;
RUN;

PROC TRANSPOSE DATA=FINAL OUT=ALL_ PREFIX=t;
BY PARAMCD PARAM AVAL AVALC;
ID TRTPN;
VAR GRPA_1;
RUN;

DATA ADRS;
SET ADRS;
IF AVAL IN (1,2) THEN OBJRESFL=0;
ELSE IF AVAL NE . THEN OBJRESFL=1;
RUN;

PROC SORT DATA=ADRS OUT=ADRS;
BY PARAMCD PARAM TRT01PN;
RUN;

ODS TRACE ON;
PROC FREQ DATA=ADRS;
BY PARAMCD PARAM TRT01PN;
TABLES OBJRESFL/BINOMIAL (EXACT);
ODS OUTPUT BINOMIALCLS=RS1;
RUN;
ODS TRACE OFF;

DATA RS1;
SET RS1;
RANGE='('||TRIM(PUT(LowerCL,5.2))||','||TRIM(PUT(UpperCL,5.2))||')';
KEEP PARAMCD PARAM TRT01PN RANGE;
RUN;

PROC SORT DATA=RS1 OUT=RS1;
BY PARAMCD PARAM TRT01PN;
RUN;

PROC TRANSPOSE DATA=RS1 OUT=RS1_1 PREFIX=tt;
BY PARAMCD PARAM;
ID TRT01PN;
VAR RANGE;
RUN;

ODS TRACE ON;
PROC FREQ DATA=ADRS;
BY PARAMCD PARAM;
TABLES TRT01PN*OBJRESFL/RISKDIFF;
ODS OUTPUT RISKDIFFCOL1=RS2;
RUN;
ODS TRACE OFF;

DATA RS2;
SET RS2;
IF ROW EQ 'Difference';
RANGE='('||TRIM(PUT(LowerCl,5.2))||','||TRIM(PUT(UpperCL,5.2))||')';
KEEP PARAMCD PARAM RANGE;
RUN;

*******p-value***************;

ODS TRACE ON;
PROC FREQ DATA=ADRS;
BY PARAMCD PARAM;
TABLES TRT01PN*OBJRESFL/CMH;
ODS OUTPUT CMH=CMH (WHERE=(AltHypothesis='Row Mean Scores Differ'));
RUN;
ODS TRACE OFF;

DATA CMH;
SET CMH;
PVALUE=TRIM(PUT(prob,PVALUE.3));
KEEP PARAMCD PARAM PVALUE;
RUN;

DATA FINAL;
MERGE ALL_ RS1_1 RS2 CMH;
BY PARAMCD;
RUN;

TITLE1 J=L "AIRIS PHARMA Private Limited.";
TITLE2 J=L "Protocol: 043-1810";
TITLE3 J=C "Table 14.1.22  Best overall response (Safety Population)";

FOOTNOTE1 J=L "CR = complete response, PR = partial response, SD = stable disease, PD = progressive disease.";
FOOTNOTE2 J=L "E:\ROSHE30730\PROGRAMS\TAB10_1_AH_20221001.SAS";

OPTIONS ORIENTATION=LANDSCAPE;
ODS ESCAPECHAR'^';
ODS RTF FILE = "E:\ROSHE30730\OUTPUTS\14_1_22.rtf" STYLE=styles.test;

PROC REPORT DATA=final NOWD SPLIT="|" MISSING
STYLE={OUTPUTWIDTH=100%} SPACING=1 WRAP
STYLE(HEADER)=[JUST=L];

COLUMN PARAMCD PARAM AVAL AVALC T1 T2 TT1 TT2 RANGE PVALUE;

DEFINE PARAMCD/ORDER NOPRINT;
DEFINE PARAM/ORDER "Parameter"
STYLE(COLUMN)=[JUST=LEFT CELLWIDTH=20%]
STYLE(HEADER)=[JUST=LEFT CELLWIDTH=20%]
;

DEFINE AVAL/ORDER NOPRINT;
DEFINE AVALC/ORDER ""
STYLE(COLUMN)=[JUST=LEFT CELLWIDTH=10%]
STYLE(HEADER)=[JUST=LEFT CELLWIDTH=10%]
;

DEFINE t1/DISPLAY "DRUG A|(N = &n1)"
STYLE(COLUMN)=[JUST=LEFT CELLWITDH=5%]
STYLE(HEADER)=[JUST=LEFT CELLWIDTH=5%]
;

DEFINE t2/DISPLAY "DRUG B|(N = &n2)"
STYLE(COLUMN)=[JUST=LEFT CELLWITDH=5%]
STYLE(HEADER)=[JUST=LEFT CELLWIDTH=5%]
;

DEFINE tt1/DISPLAY "DRUG A|(95% CI)"
STYLE(COLUMN)=[JUST=LEFT CELLWITDH=10%]
STYLE(HEADER)=[JUST=LEFT CELLWIDTH=10%]
;

DEFINE tt2/DISPLAY "DRUG B|(95% CI)"
STYLE(COLUMN)=[JUST=LEFT CELLWITDH=10%]
STYLE(HEADER)=[JUST=LEFT CELLWIDTH=10%]
;

DEFINE RANGE/DISPLAY "95% CI|Difference"
STYLE(COLUMN)=[JUST=LEFT CELLWIDTH=10%]
STYLE(HEADER)=[JUST=LEFT CELLWIDTH=10%]
;

DEFINE PVALUE/DISPLAY "P-Value"
STYLE(COLUMN)=[JUST=LEFT CELLWIDTH=5%]
STYLE(HEADER)=[JUST=LEFT CELLWIDTH=5%]
;

COMPUTE BEFORE _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPCOLOR=BLACK BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

COMPUTE AFTER _PAGE_;
LINE@1 "^{STYLE [OUTPUTWIDTH=100% BORDERTOPCOLOR=BLACK BORDERTOPWIDTH=0.5PT]}";
ENDCOMP;

COMPUTE BEFORE PARAMCD;
LINE '';
ENDCOMP;

RUN;

ODS _ALL_ CLOSE;
